when RULE_INIT priority 900 {
    # akamai source version
    set static::akamai_source_version "f5-irule:1.5.5"
    # Log debug locally to /var/log/ltm? 1=yes, 0=no
	set static::akamai_hsl_debug 0
	# Pool name to post the API activity data to
	set static::akamai_hsl_pool "/Common/akamai-collector-pool"
	# Maximum payload size
	set static::akamai_max_payload 10240
}

when CLIENT_ACCEPTED priority 900 {
	set requestTime [clock clicks -milliseconds]
	if { [PROFILE::exists clientssl] == 1 } { 
        set requestScheme "https" 
    } else {
        set requestScheme "http"
    }
	if {[active_members $static::akamai_hsl_pool]==0} {
		log local0. "[IP::client_addr]:[TCP::client_port]: [virtual name] $static::akamai_hsl_pool down, not logging"
		set bypass 1
	} else {
		set bypass 0
        # Open a new HSL connection if one is not available
	    set hsl [HSL::open -proto TCP -pool $static::akamai_hsl_pool]
	    if {$static::akamai_hsl_debug} {log local0. "[IP::client_addr]:[TCP::client_port]: New hsl handle: $hsl"} 
	}
}

when HTTP_REQUEST priority 900 {
	# If the HSL pool is down, do not run more code here
	if { not ($bypass) && not ([HTTP::has_responded]) } {

	    set reqJsonArrayStr "\{"
        foreach headerName [HTTP::header names] {
            append reqJsonArrayStr "\"$headerName\": \"[URI::encode [HTTP::header value $headerName]]\"," 
	    }
	    set reqJsonArrayStr [string trimright $reqJsonArrayStr ,] 
	    append reqJsonArrayStr "\}"

    	set http_request [HTTP::request]
	    set req_data(payload) ""
	    set req_data(source) \"$static::akamai_source_version\"
	    set req_data(headers) $reqJsonArrayStr
	    set req_data(scheme) \"$requestScheme\"
	    set req_data(method) \"[HTTP::method]\"
	    set req_data(uri) \"[HTTP::uri]\"
        set req_data(clientip) \"[IP::client_addr]\"

        # Check for POST/PATCH/PUT requests
        if {[HTTP::method] eq "POST" || [HTTP::method] eq "PATCH" || [HTTP::method] eq "PUT" } {
			if { [HTTP::header exists "Content-Length"] } {
				if { [HTTP::header "Content-Length"] > $static::akamai_max_payload }{
					set collectsize $static::akamai_max_payload
				} else {
					set collectsize [HTTP::header "Content-Length"]
				}
			} else {
				set collectsize 0
			}
			if { [ catch { HTTP::collect $collectsize } ] } {
				log local0. "Error collecting request payload"
			}
		}
	}
}

when HTTP_REQUEST_DATA priority 900 {
    if { not ($bypass)  && not ([HTTP::has_responded]) } {
        if {[HTTP::payload length] > 0} {
		    set req_data(payload) [HTTP::payload]
	    }
	}
}

when HTTP_RESPONSE priority 900 {
    if { not ($bypass)  && not ([HTTP::has_responded]) } {
		if { ([HTTP::header exists "Transfer-Encoding"]) && ([HTTP::header "Transfer-Encoding"] eq "chunked") } {
			# Response is chunked. Collect minimal amount of data so "HTTP_RESPONSE_DATA" event gets call
			set res_chunked 1

			if { [HTTP::header exists "Content-Length"] } {
				if { [HTTP::header "Content-Length"] > $static::akamai_max_payload }{
					set collectsize $static::akamai_max_payload
				} else {
					set collectsize [HTTP::header "Content-Length"]
				}
			} else {
				set collectsize 0
			}
			if { [ catch { HTTP::collect $collectsize } ] } {
				log local0. "Error collecting chunked response payload"
			}
		} else {
			set res_chunked 0
			if { [HTTP::header exists "Content-Length"] } {
				if { [HTTP::header "Content-Length"] > $static::akamai_max_payload }{
					set collectsize $static::akamai_max_payload
				} else {
					set collectsize [HTTP::header "Content-Length"]
				}
			} else {
				set collectsize 0
			}
			if { [ catch { HTTP::collect $collectsize } ] } {
				log local0. "Error collecting response payload"
			}
		}
	}
}

when HTTP_RESPONSE_DATA priority 900 {
    if { not ($bypass)  && not ([HTTP::has_responded]) } {
        # Formatting HTTP request data
        set req_data_msg "\"headers\": $req_data(headers), \
            \"virtualServerName\": \"[URI::encode [virtual name] ]\", \
		    \"scheme\": $req_data(scheme), \
		    \"method\": $req_data(method), \
		    \"requestTimestamp\": $requestTime, \
		    \"uri\": $req_data(uri), \
		    \"clientip\": $req_data(clientip), \
		    \"source\": $req_data(source)"

        if { [string length $req_data(payload)] > 0 } {
            set req_data_msg [ concat $req_data_msg ",\"payload\": \"[URI::encode $req_data(payload)]\"" ]
	    }
	    set req_data_msg [ concat "\"request\": { $req_data_msg }" ]

        # Formatting HTTP response data
	    set resJsonArrayStr "\{"
        foreach headerName [HTTP::header names] {
            append resJsonArrayStr "\"$headerName\": \"[URI::encode [HTTP::header value $headerName]]\"," 
	    }
	    set resJsonArrayStr [string trimright $resJsonArrayStr ,] 
	    append resJsonArrayStr "\}"

		set res_data_msg "\"headers\": $resJsonArrayStr, \ 
		    \"responseTimestamp\": [clock clicks -milliseconds], \
		    \"status\": [HTTP::status]"

	    if {not $res_chunked} {
            set res_data_msg [ concat $res_data_msg ",\"payload\": \"[URI::encode [HTTP::payload] ]\"" ]
	    }
	    set res_data_msg [ concat "\"response\": { $res_data_msg }" ]

	    # URI Encode the whole message to ensure message sent as single log event
        if { [ catch { HSL::send $hsl "[URI::encode "{$req_data_msg, $res_data_msg}"]\n" } ] } {
            log local0. "Error sending data to HSL"
        }
	}
}

when HTTP_RESPONSE_RELEASE priority 900 {
    if { [info exists req_data] } { catch { unset req_data }  }
	if { [info exists req_data_msg] } { catch { unset req_data_msg } }
	if { [info exists res_data_msg] } { catch { unset res_data_msg } }
	if { [info exists reqJsonArrayStr] } { catch { unset reqJsonArrayStr } }
	if { [info exists resJsonArrayStr] } { catch { unset resJsonArrayStr } }
}